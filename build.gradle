buildscript {
	ext {
		kotlinVersion = '1.0.0-beta-4589'
		cucumberVersion = '1.2.4'
		cakeParseVersion = '1.0.1'
		junitVersion = '4.12'
	}
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

plugins {
	id "com.jfrog.bintray" version "1.6"
	id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group = 'me.sargunvohra.lib'

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib',  version: kotlinVersion
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
	compile group: 'me.sargunvohra.lib', name: 'CakeParse', version: cakeParseVersion
	
	testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlinVersion
	testCompile group: 'info.cukes', name: 'cucumber-junit', version: cucumberVersion
	testCompile group: 'info.cukes', name: 'cucumber-java8', version: cucumberVersion
}

Properties properties = new Properties()
if(project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {   
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
	
	configurations = ['archives']
	
    pkg {
        repo = 'maven'
        name = 'Klap'
		desc = 'Simple annotation based command line parsing library for Kotlin.'
		websiteUrl = 'http://sargunvohra.me/Klap/'
		issueTrackerUrl = 'https://github.com/sargunster/Klap/issues'
		vcsUrl = 'https://github.com/sargunster/Klap.git'
		licenses = ['Apache-2.0']
		labels = ['kotlin', 'command', 'line', 'argument', 'library']
		publicDownloadNumbers = true
		publish = true
		version {
			name = project.version
			released = new Date()
		}
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact jar
			artifact sourcesJar
			artifact javadocJar
            groupId project.group
            artifactId 'Klap'
            version project.version
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload